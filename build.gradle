buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }
    dependencies {
        classpath 'io.github.spring-roll:roll-dev-configs:0.0.7.RELEASE'
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url 'https://maven.pkg.github.com/AlphaHinex/spring-roll'
            credentials(PasswordCredentials) {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_OWNER")
                password = project.findProperty("gpr.reader") ?: System.getenv("GH_PKG_READER")
            }
        }
    }
}

apply plugin: 'idea'

group = 'alpha.hinex.demo'
version = '0.0.1-SNAPSHOT'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'groovy'

    apply from: this.getClass().getClassLoader().getResource("META-INF/configs/dependencies.gradle")

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = rootProject.group
    version = rootProject.version

    dependencies {
            implementation platform("org.springframework.boot:spring-boot-dependencies:${versions.spring_boot}")
            implementation 'javax.servlet:javax.servlet-api:4.0.1'

        testImplementation libraries.spring_boot_starter_test
        testImplementation libraries.roll_test
    }

}